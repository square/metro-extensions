pluginManagement {
  includeBuild('build-logic')
  includeBuild('gradle-plugin')
  if (settings.hasProperty('useLocalMetro')) {
    includeBuild(settings.hasProperty('localMetroPath') ? localMetroPath : '../metro')
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

includeBuild('gradle-plugin') {
  dependencySubstitution {
    substitute module("$GROUP:gradle-plugin") using project(':')
  }
}

if (settings.hasProperty('useLocalMetro')) {
  def metroPath = settings.hasProperty('localMetroPath') ? localMetroPath : '../metro'
  includeBuild(metroPath) {
    dependencySubstitution {
      substitute module('dev.zacsweers.metro:compiler') using project(':compiler')
      substitute module('dev.zacsweers.metro:runtime') using project(':runtime')
    }
  }
}

dependencyResolutionManagement {
  repositories {
    mavenCentral()
  }
}

rootProject.name = 'metro-extensions'

include ':compiler'
include ':integration-tests:app'
include ':integration-tests:lib'
include ':stubs'
